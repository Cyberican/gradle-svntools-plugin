/**
 * Publishing to Bintray (see https://github.com/etiennestuder/gradle-plugindev-plugin/):
 *
 * gradlew -i publishPluginToBintray -PbintrayUser=[username] -PbintrayApiKey=[api-key]
 */
plugins {
  id "java-gradle-plugin"
  id "groovy"
  id "idea"
  id "nu.studer.plugindev" version "1.0.6"
  id "jacoco"
  id "com.github.kt3k.coveralls" version "2.7.1"
}

group = "at.bxm.gradleplugins"
sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
  compile "org.codehaus.groovy:groovy-all:2.4.7"
  compile "org.tmatesoft.svnkit:svnkit:1.8.12"
  testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
    exclude group: "org.codehaus.groovy"
  }
}

plugindev { // https://github.com/etiennestuder/gradle-plugindev-plugin
  pluginId "at.bxm.svntools"
  pluginImplementationClass "at.bxm.gradleplugins.svntools.SvnToolsPlugin"
  pluginDescription "Provides various Subversion tasks that operate on a local SVN workspace"
  pluginLicenses "MIT"
  pluginTags "subversion", "svn", "svnkit", "scm"
  authorId "martoe"
  authorName "Martin Ehrnhoefer"
  authorEmail "martin@ehrnhoefer.com"
  projectUrl "https://github.com/martoe/gradle-svntools-plugin"
  projectInceptionYear "2014"
  done()
}

bintray { // https://github.com/bintray/gradle-bintray-plugin
  if (project.hasProperty("bintrayUser")) {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
  } else {
    user = "dummy"
    key = "dummy"
    dryRun = true
  }
  pkg {
    repo = "gradle-plugins"
    version {
      name = project.version
      desc = releaseNotes
      released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
      vcsTag = "v$project.version"
    }
  }
}
bintrayUpload.dependsOn check

if (project.hasProperty("ignoreTestFailures")) {
  test {
    ignoreFailures = ignoreTestFailures == "true"
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
  }
}
